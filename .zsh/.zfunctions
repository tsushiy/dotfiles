autoload -Uz colors && colors
autoload -Uz compinit && compinit
autoload -Uz add-zsh-hook
autoload -Uz vcs_info

autoload -Uz history-search-end
zle -N history-beginning-search-backward-end history-search-end
zle -N history-beginning-search-forward-end history-search-end
bindkey "^P" history-beginning-search-backward-end
bindkey "^N" history-beginning-search-forward-end

# see https://blog.n-z.jp/blog/2013-11-12-zsh-cdr.html
if [[ ! -e "$HOME/.cache/shell" ]]; then
  mkdir "$HOME/.cache/shell"
fi
if [[ -n $(echo ${^fpath}/chpwd_recent_dirs(N)) && -n $(echo ${^fpath}/cdr(N)) ]]; then
  autoload -Uz chpwd_recent_dirs cdr
  add-zsh-hook chpwd chpwd_recent_dirs
  zstyle ':completion:*' recent-dirs-insert both
  zstyle ':chpwd:*' recent-dirs-max 500
  zstyle ':chpwd:*' recent-dirs-default true
  zstyle ':chpwd:*' recent-dirs-file "$HOME/.cache/shell/chpwd-recent-dirs"
  zstyle ':chpwd:*' recent-dirs-pushd true
fi

# see https://qiita.com/yuyuchu3333/items/b10542db482c3ac8b059
ls-abbr() {
  if [[ -z "${PWD}" ]]; then
    return
  fi

  local ls_result="$(CLICOLOR_FORCE=1 COLUMNS=${COLUMNS} ls -aCF | sed $'/^\e\[[0-9;]*m$/d')"
  local ls_lines="$(echo ${ls_result} | wc -l | tr -d ' ')"

  if [ "${ls_lines}" -gt 8 ]; then
    echo "${ls_result}" | head -n 4
    echo "..."
    echo "${ls_result}" | tail -n 4
    echo "$(command ls -1 -A | wc -l | tr -d ' ') files exist."
  else
    echo "${ls_result}"
  fi
}
add-zsh-hook chpwd ls-abbr

# see https://qiita.com/yuyuchu3333/items/e9af05670c95e2cc5b4d
ls-abbr-gitstatus() {
  if [ -n "${BUFFER}" ]; then
    zle accept-line
    return
  fi
  echo
  ls-abbr
  echo
  if [ "$(git rev-parse --is-inside-work-tree 2> /dev/null)" = "true" ]; then
    echo -e "\e[0;33m--- git status ---\e[0m"
    git status -sb
    echo
  fi
  zle reset-prompt
}
zle -N ls-abbr-gitstatus
bindkey '^m' ls-abbr-gitstatus

# see https://qiita.com/strsk/items/9151cef7e68f0746820d
fzf-ghq() {
  local selected_dir=$(ghq list -p | fzf --query "$LBUFFER")
  if [ -n "${selected_dir}" ]; then
    BUFFER="cd ${selected_dir}"
    zle accept-line
  fi
  zle reset-prompt
}
zle -N fzf-ghq
bindkey '^]' fzf-ghq

fzf-cdr() {
  local selected_dir=$(cdr -l | sed 's/^[0-9]* *//' | fzf --query "$LBUFFER")
  if [ -n "${selected_dir}" ]; then
    BUFFER="cd ${selected_dir}"
    zle accept-line
  fi
  zle reset-prompt
}
zle -N fzf-cdr
bindkey '^n' fzf-cdr
